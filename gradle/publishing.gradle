buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'maven-publish'
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'gradle-bitbucket-rest-plugin')
                root.appendNode('description', 'Gradle plugin for working with Bitbuckets REST API.')
                root.appendNode('url', 'https://github.com/cdancy/gradle-bitbucket-rest-plugin')
                root.appendNode('inceptionYear', '2017')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/cdancy/gradle-bitbucket-rest-plugin')
                scm.appendNode('connection', 'scm:https://cdancy@github.com/cdancy/gradle-bitbucket-rest-plugin.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/cdancy/gradle-bitbucket-rest-plugin.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def cdancy = developers.appendNode('developer')
                cdancy.appendNode('id', 'cdancy')
                cdancy.appendNode('name', 'Christopher Dancy')
            }
        }
    }
}

bintray {

    user = resolveProperty('bintrayUsername')
    key = resolveProperty('bintrayKey')
    publications = ['mavenJava']
    publish = true

    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-bitbucket-rest-plugin'
        desc = 'Gradle plugin for working with Bitbuckets REST API.'
        websiteUrl = "https://github.com/cdancy/${project.name}"
        issueTrackerUrl = "https://github.com/cdancy/${project.name}/issues"
        vcsUrl = "https://github.com/cdancy/${project.name}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'bitbucket', 'rest', 'plugin', 'gradle-bitbucket-rest-plugin']
        publicDownloadNumbers = true
        githubRepo = "cdancy/${project.name}"
        githubReleaseNotesFile = 'RELEASE_NOTES.md'

        version {
            released  = new Date()
            vcsTag = "v$project.version"
            attributes = ['gradle-plugin': ["gradle-bitbucket-rest-plugin"]]

            gpg {
                sign = true
                passphrase = resolveProperty('gpgPassphrase')
            }
        }
    }
}

String resolveProperty(String propKey) {
    String propValue = System.getenv()[propKey]
    if(propValue) { return propValue }
    hasProperty(propKey) ? getProperty(propKey) : null
}
